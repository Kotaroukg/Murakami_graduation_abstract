\appendix
\chapter{プログラムのソースコード}

\begin{framed}
{\small
\begin{verbatim}
float[][] ball_p = new float[81][2];
float[][] pre_p = new float[81][2];
float[][] ball_f	 = new float[81][2];
float[] mouse_p =new float[2];
float ball_size = 0.2;
float m=0.2;
float h=0.001;
int n = 9;
float slider_n = n;
boolean slider_dragged = false;
boolean slider2_dragged = false;

int click_ball=0;
boolean solid_mode = false;
boolean force_mode = false;

void setup(){
  size(800, 550);
  set_particle();
}

void set_particle(){
  int i, j, k=0;
  for(i=1; i<10; i++){
    for(j=1; j<10; j++){
      if(k==n) break;
      ball_p[k][0] = i;
      ball_p[k][1] = j;
      pre_p[k][0] = i;
      pre_p[k][1] = j;
      k++;
    }
  }

  for(i=0; i<n; i++){
    ball_f[i][0] = 0; 
    ball_f[i][1] = 0;
  } 
}

float distance(float[] a, float[] b){
  return sqrt( sq(a[0]-b[0]) + sq(a[1]-b[1]) );
}

float[] lennard(float p1[], float p2[]){
  float d, force;
  float[] force_xy = new float[2];
  d = distance(p1, p2);
  force = -30 * pow(d,-13) + 30 * pow(d,-7);
  force_xy[0] = force * (p2[0] - p1[0])/d; 
  force_xy[1] = force * (p2[1] - p1[1])/d;
  return force_xy;  
}

float[] verlet(float current[], float previous[], float force[]){
  float[] newpos = new float[2];
  newpos[0]=2*current[0]-previous[0]+h*h/m*force[0];
  newpos[1]=2*current[1]-previous[1]+h*h/m*force[1];
  return newpos;
} 

void reflect(int i){
  if(ball_p[i][0]<ball_size){
  ball_p[i][0] = ball_size*2-ball_p[i][0];
  pre_p[i][0] = ball_size*2-pre_p[i][0];
  }  
  else if(ball_p[i][0]>10-ball_size){
    ball_p[i][0] = (10-ball_size)*2-ball_p[i][0];
    pre_p[i][0] = (10-ball_size)*2-pre_p[i][0];
  }
    
  if(ball_p[i][1]<ball_size){
    ball_p[i][1] = ball_size*2-ball_p[i][1];
    pre_p[i][1] = ball_size*2-pre_p[i][1];
  }
  else if(ball_p[i][1]>10-ball_size && solid_mode)
    ball_p[i][1]=10-ball_size-0.01;
  else if(ball_p[i][1]>10-ball_size){
    ball_p[i][1] = (10-ball_size)*2-ball_p[i][1];
    pre_p[i][1] = (10-ball_size)*2-pre_p[i][1];
  }
}

void draw_particle(int i){
  float v;
  v = distance(ball_p[i], pre_p[i]);
  colorMode(HSB,360,100,100);
  fill(230+5000*v,100,100);  
  ellipse(ball_p[i][0]*50,ball_p[i][1]*50, ball_size*100,ball_size*100);
}
  
void inter_force(int i, int j){
  float[] tmp= new float[2];
  tmp = lennard(ball_p[i], ball_p[j]);
  ball_f[i][0] += tmp[0];
  ball_f[i][1] += tmp[1];
  ball_f[j][0] += -tmp[0];
  ball_f[j][1] += -tmp[1];
}

void calc_verlet(int i){
  float[] tmp= new float[2];
  if(solid_mode) ball_f[i][1]+= 0.00001/(h*h);
  tmp = ball_p[i];
  ball_p[i] = verlet(ball_p[i], pre_p[i],ball_f[i]);
  pre_p[i] = tmp; 
}

void force_reset(int i){
  ball_f[i][0] = 0; 
  ball_f[i][1] = 0;
}

void draw_force(int i){
  float force_sqrt_x, force_sqrt_y;
  strokeWeight(3);
  if(ball_f[i][0]>0) force_sqrt_x = sqrt(abs(ball_f[i][0]));
  else force_sqrt_x = -sqrt(abs(ball_f[i][0]));
  if(ball_f[i][1]>0) force_sqrt_y = sqrt(abs(ball_f[i][1]));
  else force_sqrt_y = -sqrt(abs(ball_f[i][1]));
  line(ball_p[i][0]*50, ball_p[i][1]*50,
    ball_p[i][0]*50+force_sqrt_x, ball_p[i][1]*50+force_sqrt_y);
  strokeWeight(1);
}

void slider(int position_x, int position_y, int slider_height,
  int slider_width, int min, int max, int number_tickmarks){
  
  int num_separator = (int)map(slider_n, min, max, 0, number_tickmarks-1);
  float cordinate_y = position_y + slider_height -
    ((float)slider_height / (number_tickmarks-1))*num_separator;
  fill(255);
  noStroke();
  rect(position_x, position_y + slider_height, slider_width+50, 30); 
  stroke(1);
  rect(position_x, position_y, slider_width, slider_height);
  fill(0);
  rect(position_x, cordinate_y, slider_width, position_y +
    slider_height - cordinate_y);
  
  text( "N="+nf(slider_n,1,0), position_x, position_y + slider_height + 20);
  if(mousePressed==false) slider_dragged=false;
  if(mouseX >= position_x & mouseX <= position_x + slider_width &
    mouseY<=cordinate_y+10 & mouseY>= cordinate_y-10){
    if(mousePressed){
      slider_dragged= true;
    }
   }
   
   if(slider_dragged){
     num_separator+= (int)((cordinate_y - mouseY)/
       ((float)slider_height / (number_tickmarks-1)));
     num_separator = constrain(num_separator, 0, number_tickmarks-1);
     slider_n=map(num_separator, 0, number_tickmarks-1, min, max);
   }
}

void slider2(int position_x, int position_y, int slider_height,
  int slider_width, float min, float max, int number_tickmarks){
  
  int num_separator =
    (int)map(h, min, max, 0, number_tickmarks-1);
  float cordinate_y = position_y + slider_height -
    ((float)slider_height / (number_tickmarks-1))*num_separator;
  fill(255);
  noStroke();
  rect(position_x, position_y + slider_height, slider_width+50, 30); 
  stroke(1);
  rect(position_x, position_y, slider_width, slider_height);
  fill(0);
  rect(position_x, cordinate_y, slider_width, position_y +
    slider_height - cordinate_y);
  
  text( "h="+nf(h,1,4), position_x, position_y + slider_height + 20);
  
  if(mousePressed==false) slider2_dragged=false;
  if(mouseX >= position_x & mouseX <= position_x + slider_width &
    mouseY<=cordinate_y+10 & mouseY>= cordinate_y-10){
    if(mousePressed){
      slider2_dragged= true;
    }
   }
   
   if(slider2_dragged){
     num_separator+= (int)((cordinate_y - mouseY)/
       ((float)slider_height / (number_tickmarks-1)));
     num_separator = constrain(num_separator, 0, number_tickmarks-1);
     h=map(num_separator, 0, number_tickmarks-1, min, max);
   }
}

void mousePressed() {
  int i;
  for(i=0; i<n; i++){
    if(mouseX-10-20<=ball_p[i][0]*50 & mouseX-10+20>= ball_p[i][0]*50 &
    mouseY-10-20<=ball_p[i][1]*50 & mouseY-10+20>= ball_p[i][1]*50){
      click_ball = i;
      fill(0);
      //Processing
      ellipse(pre_p[i][0]*50,pre_p[i][1]*50,ball_size*100,ball_size*100); 
      
      /*JavaScript
      ellipse(10+pre_p[i][0]*50,10+pre_p[i][1]*50,
        ball_size*100,ball_size*100); 
      */
      mouse_p[0] = mouseX;
      mouse_p[1] = mouseY;
      return;
    }
  }
  click_ball = -1;
}

void mouseReleased(){
  if(click_ball != -1){
    ball_f[click_ball][0] +=( mouseX - mouse_p[0])*0.0001/(h*h);
    ball_f[click_ball][1] += (mouseY - mouse_p[1])*0.0001/(h*h);
  }
}

void keyPressed() {  
  if (key == 's'||key == 'S') {
    if(solid_mode) solid_mode = false;
    else solid_mode = true;
  }
      if (key == 'f'||key == 'F') {
    if(force_mode) force_mode = false;
    else force_mode = true;
  }
  if (key == 'r'||key == 'R') set_particle();
}

void draw(){
  colorMode(RGB,256);
  int i;
  int j;
  translate(10,10);
  if (mousePressed == false){
    if(n!=slider_n) {
      n=(int)slider_n;
      set_particle();
    }
    background(255);
    text("Press R to Reset  ",550,35);
    text("Press S to Solid-Mode   "+solid_mode,550,50);
    text("Press F to Force-Mode  "+force_mode,550,65);
    fill(255,255,255);
    rect(0, 0, 500, 500);
    
    
    for( i=0; i<n; i++){
      reflect(i);
      draw_particle(i);
    }
    
    for( i=0; i<n-1; i++){
      for( j=i+1; j<n; j++){
        inter_force(i, j);
      }
    }
    
    for( i=0; i<n; i++){
      if(force_mode){
        draw_force(i);
      }
      calc_verlet(i); 
    }
    
    for(i=0; i<n; i++){
      force_reset(i);  
    }

  }
  
  translate(-10, -10);
  colorMode(RGB,256);
  slider(600, 100, 300, 20, 1, 81, 81);
  slider2(700, 100, 300, 20, 0.0001, 0.01, 100);
  translate(10, 10);
}
\end{verbatim}}
\end{framed}